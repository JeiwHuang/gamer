reset


# target files
# -----------------------------------------------------------
FILE_IN_PREFIX  = 'Diag'
FILE_IN_SUFFIX  = ''
FILE_OUT_PREFIX = 'Fig__TwoMassProfile'
ELBDM_ETA1      = 1.0
ELBDM_ETA2      = 4.0
Height1         = 1.0
Width1          = 15.0 
Height2         = 1.0
Width2          = 15.0
BOX_SIZE        = 384.0    # simulation box size (BOX_SIZE)
START_ID        = 0
END_ID          = 50
DELTA_ID        = 1
PAUSE           = -1.0     # <= 0.0 --> mouse


# terminal
# -----------------------------------------------------------
 set term x11 enhanced
#set term png enhanced crop size 1280,800
#set term postscript eps enhanced color 'Helvetica' 16


# plot style
# -----------------------------------------------------------
#set log xy
 set key reverse Left spacing 2 right
#set key reverse Left spacing 1 at first 1.5e2, 1e8
#set xlabel "r (kpc)"
#set ylabel "Density"
#set format x "10^{%T}"
#set format y "10^{%T}"
 set pointsize 1.0
 set size square
 set yrange [-1:1.0001]
 set xrange [-1.5*BOX_SIZE:1.5*BOX_SIZE]


# set the output file extension automatically
# -----------------------------------------------------------
if      ( GPVAL_TERM eq 'pngcairo'   ) FILE_OUT_EXT='png'; \
else if ( GPVAL_TERM eq 'postscript' ) FILE_OUT_EXT='eps'; \
else if ( GPVAL_TERM ne 'x11'        ) print 'Unkown terminal for settting the output file extension !!'; quit



# loop over all files
# -----------------------------------------------------------
print "Start plotting ..."

do for [ID=START_ID:END_ID:DELTA_ID] {

#  set the input and output filenames
   FILE_IN = sprintf( '%s_%06d', FILE_IN_PREFIX, ID, FILE_IN_SUFFIX )

   if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d.%s', FILE_OUT_PREFIX, ID, FILE_OUT_EXT ) }


#  load the physical time
   DUMP_TABLE   = './Record__Dump'
   NHEADER      = 1
   LOAD_TIME    = sprintf( "awk '{if(NR==%d+%d) {print $2}}' %s", NHEADER, ID+1, DUMP_TABLE  )
   TIME         = system( LOAD_TIME )*1.0    # *1.0 --> convert string to number


#  set title
   set title sprintf( 'DataID %06d (t = %9.3e)', ID, TIME )
   print sprintf( '   Plotting DataID %06d (t = %9.3e) ...', ID, TIME )


#  plot
   center = 0.5*BOX_SIZE
   P1     = 2.0*Width1*3**0.5
   P2     = 2.0*Width2*3**0.5
   t1     = TIME/ELBDM_ETA1
   t2     = TIME/ELBDM_ETA2
   A1     = Width1**4+t1**2
   A2     = Width2**4+t2**2
   B1     = 0.5*Width1**2/A1
   B2     = 0.5*Width2**2/A2 
   C1     = Height1*Width1**3*(1/A1)**(0.75)
   C2     = Height2*Width2**3*(1/A2)**(0.75)
   D1     = 0.5*t1/A1
   D2     = 0.5*t2/A2
   E1     = 1.5*atan(t1/Width1**2)
   E2     = 1.5*atan(t2/Width2**2)

   plot FILE_IN  u (($4-center)*3**0.5):($7**0.5)  w p pt 6 lc 5 title 'Dens^{1/2}' \
       ,FILE_IN  u (($4-center)*3**0.5):($8     )  w p pt 7 lc 6 title 'Real1' \
       ,FILE_IN  u (($4-center)*3**0.5):($9     )  w p pt 8 lc 7 title 'Imag1' \
       ,FILE_IN  u (($4-center)*3**0.5):($10     ) w p pt 9 lc 8 title 'Real2' \
       ,FILE_IN  u (($4-center)*3**0.5):($11     ) w p pt 10 lc 9 title 'Imag2'\
       ,C1*exp(-(x+P1)**2*B1)*cos((x+P1)**2*D1-E1)      w l      lc 10 title 'Ana R1'\
       ,C1*exp(-(x+P1)**2*B1)*sin((x+P1)**2*D1-E1)      w l      lc 11 title 'Ana I1'\
       ,C2*exp(-(x-P1)**2*B2)*cos((x-P1)**2*D2-E2)      w l      lc 12 title 'Ana R2'\
       ,C2*exp(-(x-P1)**2*B2)*sin((x-P1)**2*D2-E2)      w l      lc 13 title 'Ana I2'         


   if ( GPVAL_TERM eq 'x11' ) { if ( PAUSE <= 0.0 ) { pause mouse }   else { pause PAUSE } };

} #do for [ID=START_ID:END_ID:DELTA_ID]


print "Done";
if ( GPVAL_TERM eq 'x11' ) { pause -1 };


